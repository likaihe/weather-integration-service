
plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'org.openapi.generator' version '7.15.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14")

    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly  'org.junit.platform:junit-platform-launcher'

}


def openApiOutputDir = "$buildDir/generated/openapi"

import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

tasks.named('openApiGenerate', GenerateTask) {
    generatorName = 'spring'
    inputSpec     = "$projectDir/src/main/resources/openapi/api.yml"
    outputDir     = openApiOutputDir

    apiPackage     = 'com.weatherintegration.api'
    modelPackage   = 'com.weatherintegration.model'
    invokerPackage = 'com.weatherintegration.invoker'

    additionalProperties = [
            useSpringBoot3          : 'true',
            dateLibrary             : 'java8',
            interfaceOnly           : 'false',
            delegatePattern         : 'true',
            useTags                 : 'true',
            hideGenerationTimestamp : 'true',
            serializableModel       : 'true',
            performBeanValidation   : 'true',
            basePackage             : 'com.weatherintegration',
            documentationProvider   : 'springdoc',
            openApiNullable         : 'false',
            useOptional             : 'false'
    ]


    globalProperties = [
            apis            : '',
            models          : '',
            supportingFiles : ''
    ]

    outputs.dir("${openApiOutputDir}/src/main/java")
    outputs.dir("${openApiOutputDir}/src/main/resources")
}


sourceSets {
    main {
        java.srcDir "${openApiOutputDir}/src/main/java"
        resources.srcDir "${openApiOutputDir}/src/main/resources"
    }
}


tasks.named('compileJava') { dependsOn tasks.named('openApiGenerate') }
tasks.named('processResources') { dependsOn tasks.named('openApiGenerate') }

test { useJUnitPlatform() }
